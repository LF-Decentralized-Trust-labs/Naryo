plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'application'
    id 'com.diffplug.spotless' version '7.0.2'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'io.freefair.lombok' version '8.12.1'
    id 'maven-publish'
}

description = 'Naryo base project'

allprojects {
    apply {
        [
            'java',
            'javadoc',
            'spotless',
            'jacoco',
            'repositories'
        ].each { buildScript ->
            from("$rootDir/gradle/$buildScript/build.gradle")
        }
    }

    tasks.withType(Test).tap {
        configureEach {
            reports.html.destination(file("${reporting.baseDir}/${name}"))
            useJUnitPlatform()
        }
    }

    plugins.withType(PublishingPlugin).tap {
        configureEach {
            publishing {
                repositories {
                    maven {
                        name = "GitHubPackages"
                        url = uri("ghcr.io/lf-decentralized-trust-labs/naryo")
                        credentials {
                            username = System.getenv("GITHUB_ACTOR")
                            password = System.getenv("GITHUB_TOKEN")
                        }
                    }
                }
            }
        }
    }
}

tasks.named("spotlessJava").configure {
    dependsOn("spotlessGroovyGradle")
    mustRunAfter("bootStartScripts", "startScripts")
}

tasks.named('bootJar') { enabled = false }
