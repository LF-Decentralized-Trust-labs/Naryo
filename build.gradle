plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'application'
    alias(libs.plugins.spotless)
    alias(libs.plugins.springBoot)
    alias(libs.plugins.springDependencyManagement)
    alias(libs.plugins.freefairLombok)
    id 'maven-publish'
}

description = 'Naryo base project'

allprojects {
    apply {
        [
            'java',
            'javadoc',
            'spotless',
            'jacoco',
            'repositories'
        ].each { buildScript ->
            from("$rootDir/gradle/$buildScript/build.gradle")
        }
    }

    tasks.withType(Test).tap {
        configureEach {
            reports.html.destination(file("${reporting.baseDir}/${name}"))
            useJUnitPlatform()
        }
    }

    plugins.withType(PublishingPlugin).tap {
        configureEach {
            publishing {
                repositories {
                    maven {
                        name = "GitHubPackages"
                        url = uri("https://maven.pkg.github.com/LF-Decentralized-Trust-labs/Naryo")
                        credentials {
                            username = System.getenv("GITHUB_ACTOR")
                            password = System.getenv("GITHUB_TOKEN")
                        }
                    }
                }
            }
        }
    }
}

tasks.named("spotlessJava").configure {
    dependsOn("spotlessGroovyGradle")
    mustRunAfter("bootStartScripts", "startScripts")
}

tasks.named('bootJar') { enabled = false }
