plugins {
    id 'java-library'
    id 'io.freefair.lombok'
    id 'java-test-fixtures'
    id 'maven-publish'
}

ext {
    web3jVersion = '4.14.0'
    junitVersion = '5.7.1'
    bcprovVersion = '1.70'
    mockitoVersion = '5.17.0'
    okHttpVersion = '4.12.0'
    resilience4jVersion = '2.3.0'
    instancioJunitVersion = '5.4.0'
    jakartaAnnotationVersion = '2.1.1'
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"
    implementation "io.github.resilience4j:resilience4j-all:$resilience4jVersion"
    implementation "org.web3j:core:$web3jVersion"
    implementation "org.web3j:besu:${web3jVersion}"
    implementation "org.bouncycastle:bcprov-jdk15on:$bcprovVersion"
    implementation "jakarta.annotation:jakarta.annotation-api:$jakartaAnnotationVersion"

    compileOnly 'org.projectlombok:lombok'

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testFixturesApi "org.instancio:instancio-junit:$instancioJunitVersion"
}

tasks.named('test', Test) {
    useJUnitPlatform()
    maxHeapSize = '1G'

    doFirst {
        def bbAgent = configurations.testRuntimeClasspath
                .find { it.name.startsWith('byte-buddy-agent') }
        if (bbAgent) {
            jvmArgs += [
                "-javaagent:${bbAgent.absolutePath}"
            ]
            jvmArgs += [
                "-XX:+EnableDynamicAgentLoading"
            ]
        } else {
            logger.warn("⚠️  byte-buddy-agent JAR not found on testRuntimeClasspath; inline mock-maker may break on future JDKs.")
        }
    }

    testLogging {
        events "passed"
    }
}

tasks.named("spotlessJava").configure {
    dependsOn("spotlessGroovyGradle", "compileJava", "compileTestJava", "javadoc")
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'core'

            from components.java
        }
    }
}
