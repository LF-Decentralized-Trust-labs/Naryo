plugins {
    id 'java-library'
    id 'io.freefair.lombok'
    id 'java-test-fixtures'
    id 'maven-publish'
}

dependencies {
    implementation(libs.okhttp)
    implementation(libs.okhttpUrlConnection)
    implementation(libs.resilience4jAll)
    implementation(libs.web3jCore)
    implementation(libs.web3jBesu)
    implementation(libs.bcprov)
    implementation(libs.jakartaannotationApi)
    implementation(libs.jacksonDatatypejsr310)

    compileOnly(libs.lombok)

    testImplementation(libs.junitJupiter)
    testImplementation(libs.mockitoCore)
    testImplementation(libs.mockitoJunitJupiter)
    testRuntimeOnly(libs.junitPlatformLauncher)

    testFixturesApi(libs.instancioJunit)
}

tasks.named('test', Test) {
    useJUnitPlatform()
    maxHeapSize = '1G'

    doFirst {
        def bbAgent = configurations.testRuntimeClasspath
                .find { it.name.startsWith('byte-buddy-agent') }
        if (bbAgent) {
            jvmArgs += [
                "-javaagent:${bbAgent.absolutePath}",
                "-XX:+EnableDynamicAgentLoading"
            ]
        } else {
            logger.warn("⚠️  byte-buddy-agent JAR not found on testRuntimeClasspath; inline mock-maker may break on future JDKs.")
        }
    }

    testLogging {
        events "passed"
    }
}

tasks.named("spotlessJava").configure {
    dependsOn("spotlessGroovyGradle", "compileJava", "compileTestJava", "javadoc")
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'core'
            from components.java
        }
    }
}
